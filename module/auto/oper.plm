# $Id$
# copyright (c)1997-2000 Yoshinori Hasegawa <hasegawa@madoka.org>

package auto_oper;

$_ = 'auto_oper';

sub initialize {
}

sub ss_privmsg {
  local($serverno, $prefix, $cmd, @params) = @_;
  local($userno, $nick, %alias);
  $userno = $'userno[$serverno];
  $nick = &'prefix($prefix);
  %alias = &get_alias_user($userno, $prefix);
  $alias{'nick.now'} = $nick;
  if (&'channel($params[0])) {
    $alias{'channel'} = $params[0];
  }
  foreach $req (&'property($userno, 'request')) {
    next unless $params[1] =~ /$req/;
    if (!&'channel($params[0])) {
      foreach $reply (&'property($userno, 'private')) {
        &privmsg($serverno, $nick, &'format($reply, %alias));
      }
    } elsif (!&'exist($'nameslist{$serverno, $params[0]}, $nick, "\+$nick", "\@$nick")) {
      foreach $reply (&'property($userno, 'out')) {
        &privmsg($serverno, $nick, &'format($reply, %alias));
      }
    } elsif (!&'exist($'nameslist{$serverno, $params[0]}, "\@$'nick[$serverno]")) {
      foreach $reply (&'property($userno, 'notoper')) {
        &privmsg($serverno, $params[0], &'format($reply, %alias));
      }
    } elsif (&'exist($'nameslist{$serverno, $params[0]}, "\@$nick")) {
      foreach $reply (&'property($userno, 'oper')) {
        &privmsg($serverno, $params[0], &'format($reply, %alias));
      }
    } elsif (!&check_mask($userno, $prefix, $params[0])) {
      foreach $reply (&'property($userno, 'deny')) {
        &privmsg($serverno, $params[0], &'format($reply, %alias));
      }
    } else {
      foreach $reply (&'property($userno, 'message')) {
        &privmsg($serverno, $params[0], &'format($reply, %alias));
      }
      &'s_print($serverno, '', 'MODE', $params[0], '+o', $nick);
    }
    last;
  }
  return ($prefix, $cmd, @params);
}

sub privmsg {
  local($serverno, $to, $msg) = @_;
  if ($msg) {
    &'s_print($serverno, '', 'PRIVMSG', $to, $msg);
    foreach $cno (&'array($'clientlist)) {
      next unless $'server[$cno] == $serverno;
      &'c_print($cno, &'user($cno), 'PRIVMSG', $to, $msg);
    }
  }
}

sub check_mask {
  local($userno, $prefix, $chan) = @_;
  local($user, $chlist, $list, $regex);
  foreach $mask (&'property($userno, 'mask')) {
    ($user, $chlist) = split(/\s+/, $mask);
    $list = '';
    foreach $vchan (split(/\,/, $chlist || '')) {
      $list = &'add($list, &'real($vchan));
    }
    if (&'channel($chan)) {
      next unless (!$list || &'exist($list, $chan));
    } else {
      next unless (!$list || &'exist($list, '*'));
    }
    if ($user =~ /^\-/) {
      $regex = &'regex(substr($user, 1));
      return 0 if $prefix =~ /$regex/i;
    }
    if ($user =~ /^\+/) {
      $regex = &'regex(substr($user, 1));
    } else {
      $regex = &'regex($user);
    }
    return 1 if $prefix =~ /$regex/i;
  }
  return 0;
}

sub get_alias_user {
  local($userno, $from, $prefix) = @_;
  local($file, @list);
  $file = &'property($userno, 'alias');
  foreach $list (&split_list('nick', &read_file($file))) {
    @list = &'array($list);
    if (&match_alias_user($from, @list)) {
      return &parse_alias($prefix, @list);
    }
  }
  return ();
}

sub match_alias_user {
  local($from, @list) = @_;
  local($var, $arg, $regex);
  foreach $line (@list) {
    ($var, $arg) = split(/\s+/, $line, 2);
    next unless "\L$var\E" eq 'user';
    $regex = &'regex($arg);
    next unless $from =~ /$regex/i;
    return 1;
  }
  return 0;
}

sub parse_alias {
  local($prefix, @list) = @_;
  local(%alias, $var, $arg, $key);
  %alias = ();
  foreach $line (@list) {
    ($var, $arg) = split(/\s+/, $line, 2);
    $var = "\L$var\E";
    if ($prefix) {
      $key = $prefix . '.' . $var;
    } else {
      $key = $var;
    }
    next if defined($alias{$key});
    if ($var eq 'nick') {
      $alias{$key} = (split(/\,/, $arg))[0];
    } elsif ($var eq 'name') {
      $alias{$key} = (split(/\s+/, $arg))[0];
    } else {
      $alias{$key} = $arg;
    }
  }
  return %alias;
}

sub split_list {
  local($field, @list) = @_;
  local($var, $arg, @array, @entry);
  $field = "\L$field\E";
  @array = ();
  @entry = ();
  foreach $line (@list) {
    ($var, $arg) = split(/\s*\:\s*/, $line, 2);
    if ($field eq "\L$var\E") {
      push(@array, &'list(@entry)) if @entry;
      @entry = ();
    }
    push(@entry, $var . ' ' . $arg);
  }
  push(@array, &'list(@entry)) if @entry;
  return @array;
}

sub read_file {
  local($file) = @_;
  local($name, $code, $mtime, @data, $line);
  ($name, $code) = &filename($file);
  $mtime = (stat($name))[9];
  if (defined($mtime)) {
    $modify{$name} = -1 unless defined($modify{$name});
    if ($modify{$name} != $mtime) {
      if (open(TMP, $name)) {
        @data = ();
        while (defined($line = <TMP>)) {
          $line =~ s/^\s+//;
          next if $line =~ /^[\#\;]/;
          $line =~ tr/\r\n//d;
          next unless $line;
          $line =~ s/\s+$//;
          $line = &code_euc($line, $code) if $code;
          push(@data, $line);
        }
        close(TMP);
        $modify{$name} = $mtime;
        $cache{$name} = &'list(@data);
        return @data;
      }
    } else {
      return &'array($cache{$name});
    }
  }
  return ();
}

sub filename {
  local($file) = @_;
  local($idx, $name, $code);
  return ('', '') unless $file;
  if (($idx = rindex($file, ';')) != -1) {
    $name = substr($file, 0, $idx);
    $code = substr($file, $idx + 1);
  } else {
    $name = $file;
    $code = '';
  }
  return (&'expand($name), $code);
}

sub code_euc {
  local($line, $list) = @_;
  foreach $code (split(/\,/, "\L$list\E")) {
    if ($code eq 'euc') {
      $line = &'euc_euc($line);
    } elsif ($code eq 'jis') {
      $line = &'jis_euc($line);
    } elsif ($code eq 'sjis') {
      $line = &'sjis_euc($line);
    }
  }
  return $line;
}

__END__

=head1 名前

auto/oper.plm - 特定の発言をした人をチャンネルオペレータにする

=head1 説明

特定の文字列を発言した人をチャンネルオペレータにします。
何らかの理由でチャンネルオペレータにできないできない場合は、
それぞれの理由に応じた文字列を発言します。

=head1 プロパティ

=over 4

=item auto.oper.alias ファイル名[;({jis|euc|sjis})]

ユーザのエイリアスを参照するためのファイルを指定します。
ここで指定するファイルはauto/alias.plmで使うものと
同じ形式のものです。

=item auto.oper.request* 文字列

チャンネルオペレータを要求する文字列を指定します。
ここで指定した文字列を発言した人をチャンネルオペレータにします。

=item auto.oper.mask* {+|-}ユーザマスク [(チャンネル名)]

チャンネルオペレータを要求した人が、ここで指定したマスクに
マッチしなかった場合は、auto.oper.denyで指定した文字列を発言し、
チャンネルオペレータにはしません。

=item auto.oper.message* フォーマット

チャンネルオペレータを要求した人を実際にチャンネルオペレータ
にするとき、ここで指定したものを発言し、その人をチャンネル
オペレータにします。文字列中に#(nick)のように書くことで、
エイリアスの対応する値で置き換えます。
また、#(name|nick)のように「|」で区切って書くと、
まずnameの値で置換しようとし、nameが定義されていなければ
nickで置換します。エイリアス中に設定されているもの以外では、
#(nick.now)で現在のニックネームに、#(channel)で発言のあった
チャンネルにそれぞれ置換されます。

=item auto.oper.deny* フォーマット

チャンネルオペレータを要求した人がauto.oper.maskで指定したものに
マッチしなかった場合、ここで指定したものを発言します。
#(nick)のように書くことで、エイリアス中の対応する値で
置き換えます。

=item auto.oper.oper* フォーマット

チャンネルオペレータを要求した人がすでにチャンネルオペレータだった
場合、ここで指定したものを発言します。
#(nick)のように書くことで、エイリアス中の対応する値で
置き換えます。

=item auto.oper.notoper* フォーマット

チャンネルオペレータを要求されたときに、
自分がチャンネルオペレータでないためにその人をチャンネルオペレータに
できない場合、ここで指定したものを発言します。
#(nick)のように書くことで、エイリアス中の対応する値で
置き換えます。

=item auto.oper.private* フォーマット

チャンネルオペレータの要求がチャンネルに対してではなく、
自分に対してされた場合、ここで指定したものを発言します。
#(nick)のように書くことで、エイリアス中の対応する値で
置き換えます。

=item auto.oper.out* フォーマット

チャンネルオペレータの要求がチャンネルの外から発言された場合、
ここで指定したものを発言します。
#(nick)のように書くことで、エイリアス中の対応する値で
置き換えます。

=back

=head1 設定例

    + auto/oper.plm
    auto.oper.alias: alias.txt;euc
    auto.oper.request: ぷらむなると
    auto.oper.mask: +*!*@*
    auto.oper.message: は〜い☆
    auto.oper.deny: なるとはあげられませんわ。
    auto.oper.oper: #(name|nick.now)さんはもうなると持ってますよ。
    auto.oper.notoper: ごめんなさい。なると持ってないんです。
    auto.oper.private: なるとがほしいチャンネルで言ってくださいね。
    auto.oper.out: えっ、どこにいるんですか?

「ぷらむなると」と発言した人をチャンネルオペレータにします。
すべてのユーザをチャンネルオペレータにすることを許可しているので、
誰がどのチャンネルで要求してきても、その人をチャンネルオペレータに
します。何らかの理由でチャンネルオペレータにできないときは、
それぞれの理由に応じたメッセージを発言します。

    + auto/oper.plm
    auto.oper.request: なると
    auto.oper.request: なるとくれ
    auto.oper.request: なるとください
    auto.oper.request: なるとちょうだい
    auto.oper.mask: -*!*@*.com
    auto.oper.mask: +*!*@* #まどか:*.jp
    auto.oper.mask: +*!*@*.jp

チャンネルオペレータの要求が4種類ある例です。
このうちのどれかを発言した人をチャンネルオペレータにします。
また、チャンネルオペレータを要求してきたユーザを確認し、
「*!*@*.com」にマッチする人はオペレータにはしません。
また、「#まどか:*.jp」ではそれ以外のすべての人を
チャンネルオペレータにします。その他のチャンネルでは「*!*@*.jp」に
マッチする人だけをチャンネルオペレータにします。
何らかの理由でチャンネルオペレータにできないとしても何も発言しません。

=cut
